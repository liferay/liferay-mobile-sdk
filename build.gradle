buildscript {
	dependencies {
		classpath group: 'com.liferay', name: 'com.liferay.gradle.plugins.source.formatter', version: '1.0.3'
	}

	repositories {
		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}

plugins {
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.7.1'
}

apply plugin: 'com.liferay.source.formatter'

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_7

ext {
	artifactId = 'liferay-mobile-sdk'
}

group 'com.liferay.mobile'
version '2.0.1'

formatSource {
	baseDirName = 'src/main/java'
	copyrightFileName = 'LICENSE'
}

def rxVersion = '1.2.5'

dependencies {
	compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
	compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'

	compileOnly group: 'com.google.android', name: 'android', version: '2.3.3'
	compileOnly group: 'io.reactivex', name: 'rxjava', version: rxVersion

	testCompile group: 'com.liferay.mobile', name: 'liferay-mobile-sdk-services', version: '1.0'
	testCompile group: 'io.reactivex', name: 'rxjava', version: rxVersion
	testCompile group: 'junit', name: 'junit', version: '4.12-beta-3'
	testCompile group: 'org.json', name: 'json', version: '20160212'

	testRuntime group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.0.1'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifactId project.artifactId
			artifact sourceJar
		}
	}
}

bintray {
	user = findProperty('user') ?: ''
	key = findProperty('key') ?: ''

	publications = ['mavenJava']

	pkg {
		repo = 'liferay-mobile'
		name = project.artifactId
		userOrg = 'liferay'
		licenses = ['LGPL-2.1']
		vcsUrl = 'https://github.com/brunofarache/liferay-android-sdk.git'

		version {
			name = project.version
		}
	}
}

task sourceJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14.1'
}