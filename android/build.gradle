archivesBaseName = 'liferay-android-sdk'

apply from: '../formatter.gradle'
format.workingDir = 'src/main/java'

apply from: '../sdk-builder.gradle'

sourceSets {
	main {
		java {
			srcDir 'src/gen/java'
		}
	}
}

dependencies {
	compile group: 'com.google.android', name: 'android', version: '2.3.3'
	compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.4.0'
	compile group: 'org.apache.httpcomponents', name: 'httpclient-android', version: '4.3.3'
	compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.3.3'
	testCompile group: 'junit', name: 'junit', version: '4.12-beta-3'
}

configurations {
	testCompile.exclude module: 'httpclient-android'
}

task formatTest(type: JavaExec) {
	main = format.main
	classpath = format.classpath
	workingDir = 'src/test/java'
}

def excluded = "${packageName.replaceAll('\\.', '/')}/v*/**"

task jarCore(type: Jar, dependsOn: classes) {
	classifier = 'core'
	from sourceSets.main.output
	exclude(excluded)
}

task jarCoreSources(type: Jar, dependsOn: classes) {
	classifier = 'core-sources'
	from sourceSets.main.allSource
	exclude(excluded)
}